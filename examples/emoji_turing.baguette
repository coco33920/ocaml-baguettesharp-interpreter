//Program reading subroutine, the input line must like STATE-TAPESTATE-TOWRITE-DIRECTION-NEWSTATE (0-1-0-r-0 :
//when the machine is in state 0 and reads 1 in the tape it writes 0, move the head to the write and stay in the state 0
🍨 🍉 program_read 🍉
🥮 
    🌰 🧅 🍉 line 🍉 , ⛈️🍫 🧅 0 🧄 🧄
    🌰 🧅 🍉 rline 🍉 , 🧇 🧅 🧈 🧅 🍉 line 🍉 🧄 🍉 - 🍉 🧄 🧄
    🌰 🧅 🍉 temp_state 🍉 🎂🥕 🧅 🥧🍓 🧅 0 , 🧈 🧅 🍉 rline 🍉 🧄 🧄 🧄 🧄
    🌰 🧅 🍉 tapestate 🍉 🎂🥕 🧅 🥧🍓 🧅 1 , 🧈 🧅 🍉 rline 🍉 🧄 🧄 🧄 🧄
    🌰 🧅 🍉 towrite 🍉 🎂🥕 🧅 🥧🍓 🧅 2 , 🧈 🧅 🍉 rline 🍉 🧄 🧄 🧄 🧄
    🌰 🧅 🍉 direction 🍉 🥧🍓 🧅 3 , 🧈 🧅 🍉 rline 🍉 🧄 🧄 🧄
    🌰 🧅 🍉 newstate 🍉 🥧🍓 🧅 4 , 🧈 🧅 🍉 rline 🍉 🧄 🧄 🧄

    🥧🫐 🧅 🧈 🧅 🍉 tapestate 🍉 🧄 , 🥧🍓 🧅 🧈 🧅 🍉 temp_state 🍉 🧄 🧈 🧅 🍉 delta0 🍉 🧄 🧄 🧈 🧅 🍉 towrite 🍉 🧄 🧄 
    🥧🫐 🧅 🧈 🧅 🍉 tapestate 🍉 🧄 , 🥧🍓 🧅 🧈 🧅 🍉 temp_state 🍉 🧄 🧈 🧅 🍉 delta1 🍉 🧄 🧄 🧈 🧅 🍉 direction 🍉 🧄 🧄 
    🥧🫐 🧅 🧈 🧅 🍉 tapestate 🍉 🧄 , 🥧🍓 🧅 🧈 🧅 🍉 temp_state 🍉 🧄 🧈 🧅 🍉 delta2 🍉 🧄 🧄 🧈 🧅 🍉 newstate 🍉 🧄 🧄 

    🌰 🧅 🍉 i 🍉 , 🧁🍷 🧅 1 , 🧈 🧅 🍉 i 🍉 🧄 🧄 🧄

    🥠 🎂🍋 🧅 🧈 🧅 🍉 i 🍉 🧄 , 🧈 🧅 🍉 n 🍉 🧄 🧄
    🍰 🥮
        🍞 🍉 program_read 🍉
    🍪 🥖
🍪 🥖

//Step subroutine, updates the tape, head position and state of the machine
🍨 🍉 step 🍉
🥮
    🥠 🍞🍎 🧅 🥮🍵🍫 🧅 🧈 🧅 🍉 state 🍉 🧄 , 🍉 H 🍉 🧄 🧄
    🍰 🥮 
        🌰 🧅 🍉 current_tapestate 🍉 , 🎂🥕 🧅 🧇🇧🇪 🧅 🧈 🧅 🍉 head 🍉 🧄 , 🧈 🧅 🍉 tape 🍉 🧄 🧄 🧄 🧄 
        🌰 🧅 🍉 current0 🍉 , 🥧🍓 🧅 🎂🥕 🧅 🧈 🧅 🍉 state 🍉 🧄 🧄 🧈 🧅 🍉 delta0 🍉 🧄 🧄 🧄
        🌰 🧅 🍉 current1 🍉 , 🥧🍓 🧅 🎂🥕 🧅 🧈 🧅 🍉 state 🍉 🧄 🧄 🧈 🧅 🍉 delta1 🍉 🧄 🧄 🧄
        🌰 🧅 🍉 current2 🍉 , 🥧🍓 🧅 🎂🥕 🧅 🧈 🧅 🍉 state 🍉 🧄 🧄 🧈 🧅 🍉 delta2 🍉 🧄 🧄 🧄

        🌰 🧅 🍉 towrite_steptemp 🍉  , 🥧🍓 🧅 🧈 🧅 🍉 current_tapestate 🍉 🧄 , 🧈 🧅 🍉 current0 🍉 🧄 🧄 🧄
        🌰 🧅 🍉 direction_steptemp 🍉 , 🥧🍓 🧅 🧈 🧅 🍉 current_tapestate 🍉 🧄 , 🧈 🧅 🍉 current1 🍉 🧄 🧄 🧄
        🌰 🧅 🍉 newstate_steptemp 🍉 , 🥧🍓 🧅 🧈 🧅 🍉 current_tapestate 🍉 🧄 , 🧈 🧅 🍉 current2 🍉 🧄 🧄 🧄
    
        🌰 🧅 🍉 arr_tape 🍉 , 🥞 🧅 🧈 🧅 🍉 tape 🍉 🧄 🧄 🧄
        🥧🫐 🧅 🧈 🧅 🍉 head 🍉 🧄 , 🧈 🧅 🍉 arr_tape 🍉 🧄 , 🥯 🧅 🧈 🧅 🍉 towrite_steptemp 🍉 🧄 🧄 🧄
        🌰 🧅 🍉 tape 🍉 , 🇨🇦🍁 🧅 🧈 🧅 🍉 arr_tape 🍉 🧄 🧄 🧄
        🌰 🧅 🍉 state 🍉 , 🧈 🧅 🍉 newstate_steptemp 🍉 🧄 🧄

        🌰 🧅 🍉 head 🍉 , 🧁🍷 🧅 -1 , 🧈 🧅 🍉 head 🍉 🧄 🧄 🧄
        🍞🍫 🧅 🍉 Step (tape,state): (%d,%d) 🍉 , 🍩 🧅 🧈 🧅 🍉 tape 🍉 🧄 , 🍉 2 🍉 , 🍉 🍉 🧄 , 🧈 🧅 🍉 state 🍉 🧄 🧄

        🥠 🥮🍵🍫 🧅 🧈 🧅 🍉 direction_steptemp 🍉 🧄 , 🍉 * 🍉 🧄
        🍰 🥮 
            🌰 🧅 🍉 head 🍉 , 🧁🍷 🧅 1 , 🧈 🧅 🍉 head 🍉 🧄 🧄 🧄
        🍪
        🥠 🥮🍵🍫 🧅 🧈 🧅 🍉 direction_steptemp 🍉 🧄 , 🍉 r 🍉 🧄
        🍰 🥮 
            🌰 🧅 🍉 head 🍉 , 🧁🍷 🧅 2 , 🧈 🧅 🍉 head 🍉 🧄 🧄 🧄
        🍪 🥖
    🍪 🥖
🍪 🥖

🍨 🍉 run 🍉 
🥮
    🍞 🍉 step 🍉
    🥠 🍞🍎 🧅 🥮🍵🍫 🧅 🧈 🧅 🍉 state 🍉 🧄 , 🍉 H 🍉 🧄 🧄
    🍰 🥮 
        🍞 🍉 run 🍉
    🍪 🥖
🍪 🥖


//Turing machine initialization
//Creation of three 100 by 3 matrices to represent the transition function
//Declaraton of the initial state and the tape with the head at the first bit of the input
🌰 🧅 🍉 state 🍉 , 🍉 0 🍉 🧄 🥖
🥐 🧅 🍉 Please type the input tape 🍉 🧄 🥖 
🌰 🧅 🍉 tape 🍉 , 🧇🍫 🧅 🧇🍫 🧅 🎂🎄 🧅 500 , 🍉 2 🍉 🧄 , ⛈️🍫 🧅 0 🧄 🧄 , 🎂🎄 🧅 500 , 🍉 2 🍉 🧄 🧄 🧄 🥖
🌰 🧅 🍉 head 🍉 , 500 🧄 🥖
🥐 🧅 🍉 Please type the number of line of the program 🍉 🧄 🥖 
🌰 🧅 🍉 n 🍉 , ⛈️🍫 🧅 🧄 🧄 🥖 
🌰 🧅 🍉 i 🍉 , 0 🧄 🥖
🌰 🧅 🍉 delta0 🍉 , 🥧🍋 🧅 100 , 3 , 0 🧄 🥖
🌰 🧅 🍉 delta1 🍉 , 🥧🍋 🧅 100 , 3 , 0 🧄 🥖
🌰 🧅 🍉 delta2 🍉 , 🥧🍋 🧅 100 , 3 , 0 🧄 🥖

//Reading program from STDIN
🍞 🍉 program_read 🍉 🥖

//Running the machine
🍞 🍉 run 🍉 🥖

//Printing output (tape), final state (which should be H), and position of the head
🍞🍫 🧅 🍉 Final (tape,state,head): (%d,%d,%d) 🍉 , 🍩 🧅 🧈 🧅 🍉 tape 🍉 🧄 , 🍉 2 🍉 , 🍉 🍉 🧄 , 🧈 🧅 🍉 state 🍉 🧄 , 🧈 🧅 🍉 head 🍉 🧄 🧄 🥖
